/** The user's spaceship. */

class Spaceship {
    field int x, y;
    field int width, height;
    // TODO: Add lives to spaceship.
    field Bullet shotBullet;

    method Bullet getShotBullet()
    {
        return shotBullet;
    }

    method int getX()
    {
        return x;
    }

    method int getY()
    {
        return y;
    }

    method int getWidth()
    {
        return width;
    }

    method int getHeight()
    {
        return height;
    }

    method int getX2()
    {
        return x + width;
    }

    method int getY2()
    {
        return y + height;
    }
    
    constructor Spaceship new(int initialX, int initialY, int shipWidth, int shipHeight)
    {
        let x = initialX;
        let y = initialY;
        let width = shipWidth;
        let height = shipHeight;
        return this;
    }
    
    method void act()
    {
        do move();

        if (KeypressReader.spacePressed()) {
            let shotBullet = shoot();
        } else {
            let shotBullet = null;
        }

        return;
    }

    method void move()
    {
        if (KeypressReader.leftArrPressed()) {
            if (ScreenBounds.validate_x(x - 20)) {
                let x = x - 20;
            } else {
                let x = 0;
            }
        }

        if (KeypressReader.rightArrPressed()) {
            if (ScreenBounds.validate_x(x + 20 + getWidth())) {
                let x = x + 20;
            } else {
                let x = ScreenBounds.getWidth() - getWidth() - 1;
            }
        }

        return;
    }

    method Bullet shoot()
    {
        var Bullet bullet;
        var int xOrigin;

        let xOrigin = Math.divide(width, 2) + x;
        let bullet = Bullet.new(xOrigin, y);

        return bullet;
    }
}

